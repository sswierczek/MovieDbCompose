buildscript {
    ext {
        kotlin_version = '2.1.20'
        kotlin_corutines = '1.10.1'
        hilt_version = '2.55'
        compose_version = '1.6.3'
        room_version = '2.6.1'
    }

    dependencies {
        classpath "com.google.dagger:hilt-android-gradle-plugin:$hilt_version"
    }
    repositories {
        google()
        mavenCentral()
    }
}
/**
 * You should use `apply false` in the top-level build.gradle file
 * to add a Gradle plugin as a build dependency, but not apply it to the
 * current (root) project. You should not use `apply false` in sub-projects.
 * For more information, see https://docs.gradle.org/current/userguide/plugins.html#sec:subprojects_plugins_dsl
 * Applying external plugins with same version to subprojects.
 */
plugins {
    id 'com.android.application' version '8.9.0' apply false
    id 'com.android.library' version '8.9.0' apply false
    id 'org.jetbrains.kotlin.android' version "$kotlin_version" apply false
    id 'com.google.devtools.ksp' version '2.1.20-1.0.31' apply false
    id 'androidx.room' version "$room_version" apply false
    id 'org.jetbrains.kotlinx.kover' version "0.5.0"
    id 'org.jetbrains.kotlin.plugin.compose' version '2.1.20' apply false
}

static def getProperty(String prop, File propsFile) {
    if (propsFile.exists()) {
        def props = new Properties()
        props.load(new FileInputStream(propsFile))
        return props[prop]
    } else {
        return System.getenv(prop)
    }
}

ext {
    config = [
            baseUrl       : "https://api.themoviedb.org/3/",
            apiKey        : getProperty('apiKey', rootProject.file('secure/api.properties')),
            poeditorApiKey: getProperty('poeditorApiKey', rootProject.file('secure/api.properties'))
    ]
}

tasks.register('clean', Delete) {
    delete rootProject.buildDir
}